#!/usr/bin/perl
#
# Copyright 2013 - Xavier Berger - http://rpi-experiences.blogspot.fr/
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#

  #print encode_json \%{$configuration->{'config'}->{'web'}};
  #print Data::Dumper->Dump([$configuration]);

#use diagnostics;
use strict;
use IPC::ShareLite;
$|=1;
$SIG{CHLD}   = 'IGNORE';
$SIG{INT}    = sub { stop(); }; # catch Ctrl+C

my $VERSION  = "{DEVELOPMENT}";
our $verbose  = 0;
my $pidfile;

package Configuration;
use strict;
use POSIX;
use Cwd 'abs_path';
use Data::Dumper;

sub new
{
  my $this = bless { }, shift;
  $this->Debug(3,"");
  $this->{'rrd'}=();
  $this->{'daemon'}->{'confFiles'} = [];
  return $this;
}

sub Debug
{
  my $this = shift;
  my $level = shift;

  $level <= $verbose or return;
  print STDERR "[", strftime("%Y/%m/%d-%H:%M:%S", localtime), "] ", " " x (5-$level), (caller 1)[3], " @_\n";
}

sub Load
{
  my $this = shift;
  $this->Debug(2,"");

  $_ = abs_path($0);
  my ($path,$file) = /(.*)\/([^\/]*)$/;

  if ( scalar(@{$this->{'daemon'}->{'confFiles'}}) == 0 ) {
    push (@{$this->{'daemon'}->{'confFiles'}}, '/etc/rpimonitord.conf');
    @{$this->{'daemon'}->{'confFiles'}} = ( @{$this->{'daemon'}->{'confFiles'}}, glob "/etc/rpimonitord.conf.d/*.conf" ) ;
  }

  foreach ( @{$this->{'daemon'}->{'confFiles'}} ) {
    $this->LoadFile($_);
  }
 
  # Set version (used by web pagescache mechanism)
  $this->{'version'} = localtime();

  # Load default values is not defined yet defined
  $this->{'daemon'}->{'user'}        ||= "pi";
  $this->{'daemon'}->{'group'}       ||= "pi";
  $this->{'daemon'}->{'noserver'}    ||= 0;
  $this->{'daemon'}->{'port'}        ||= 8888;
  $this->{'daemon'}->{'addr'}        ||= '0.0.0.0';
  -d "$path/web" and $this->{'daemon'}->{'webroot'}     ||= "$path/web";
  $this->{'daemon'}->{'webroot'}     ||= "/usr/share/rpimonitor/web";
  $this->{'daemon'}->{'delay'}       ||= 10;
  $this->{'daemon'}->{'timeout'}     ||= 5;
  $this->{'daemon'}->{'sharedmemkey'}||= 20130906;

  # Check user and group
  $this->{'daemon'}->{'gid'} = getgrnam($this->{'daemon'}->{'user'})  || 1000;
  $this->{'daemon'}->{'uid'} = getpwnam($this->{'daemon'}->{'group'}) || 1000;

  # Check rrd directory and files and create them if they are missing
  # construct the list of rrd page accessible
  -d "$this->{'daemon'}->{'webroot'}/stat" or mkdir "$this->{'daemon'}->{'webroot'}/stat";
  -f "$this->{'daemon'}->{'webroot'}/stat/empty.rrd" or $this->CreateRRD( "$this->{'daemon'}->{'webroot'}/stat/empty.rrd", 'empty', 'GAUGE' ); 

  #die Data::Dumper->Dump([$this->{'rrd'}]);

  # manage rrds
  foreach (@{$this->{'rrd'}}){
    my @name = split (',',$_->{'name'});
    my $type = $_->{'rrd'};
    foreach (@name) {
      my $filename="$this->{'daemon'}->{'webroot'}/stat/$_.rrd";
      -f "$filename" or $this->CreateRRD($filename,$_,$type);
      push(@{$this->{'rrdlist'}},"stat/$_.rrd");
    }
  }
 
  #print Data::Dumper->Dump([$this->{'web'}]);  
 
  # manage menu
  foreach (@{$this->{'web'}->{'status'}}) {
    $_->{'name'} and push(@{$this->{'menu'}->{'status'}}, $_->{'name'});
  }
  foreach (@{$this->{'web'}->{'statistics'}}) {
    $_->{'name'} and push(@{$this->{'menu'}->{'statistics'}}, $_->{'name'});
  }

  $this->{'sharedmem'} = IPC::ShareLite->new(
        -key     => $this->{'daemon'}->{'sharedmemkey'},
        -create  => 'yes',
        -destroy => 'no'
    ) or die $!;

  if ( $this->{'show'} ) {
    die Data::Dumper->Dump([$this]);
  }
}

sub LoadFile
{
  my $this = shift;
  my $confFile = shift;
  $this->Debug(2,"Loading file: $confFile");

  open ( FILE, $confFile)
    or die "Error while openning configuration file \"$confFile\" because $!\n";
  while (<FILE>){
    chomp;
    /^\s*#|^$/ and next;                 # Ignore comments
    my ($key, $value) = /^([^=]*)=(.*)/; # Extract key and value
    my @leaves=split('\.', $key);        # Explode key and construct config hash
    my $tree=$this;
    my $current;
    my $next;
    while (scalar(@leaves) > 0) {
      $current ||= shift (@leaves);
      $next = shift (@leaves);
      if ( $next =~ /^\d+$/ ) {
        if ($current =~ /^\d+$/) {
          @{$tree}[$current-1] ||= [];
          $tree=@{$tree}[$current-1];
        }
        else {
          $tree->{$current} ||= [];
          $tree=$tree->{$current};
        }
      } else {
        if ($current =~ /^\d+$/) {
          @{$tree}[$current-1] ||= {};
          $tree=@{$tree}[$current-1];
        }
        else {
          $tree->{$current} ||= {};
          $tree=$tree->{$current};
        }
      }      
      if ( ($next eq 'rrd') and $value) { push(@{$this->{'rrd'}},$tree) };
      $current = $next;
    }
    if ($current =~ /^\d+$/) {
      @{$tree}[$current-1] = $value;
    }
    else {
      $tree->{$current} = $value;
    }
  }  
}

sub CreateRRD
{
  my $this = shift;
  my $filename = shift;
  my $name = shift;
  my $type = shift;
  $this->Debug(2,"$filename - $name - $type");

  my $current = time();
  my $start = $current - 60;

  $this->Debug(2,"$filename",
                "--start", $start,
                "--step", $this->{'daemon'}->{'delay'},
                "DS:$name:$type:600:U:U",
                "RRA:AVERAGE:0.5:1:8640",    # 1 day with interval of 10sec
                "RRA:AVERAGE:0.5:6:2880",    # 2 day with interval of 1min
                "RRA:AVERAGE:0.5:60:2016",   # 2 week with interval of 10min
                "RRA:AVERAGE:0.5:180:1488",  # 1 mounth with interval of 30min
                "RRA:AVERAGE:0.5:360:8784"   # 1 year with interval of 1hour
                );

  RRDs::create( "$filename",
                "--start", $start,
                "--step", $this->{'daemon'}->{'delay'},
                "DS:$name:$type:600:U:U",
                "RRA:AVERAGE:0.5:1:8640",    # 1 day with interval of 10sec
                "RRA:AVERAGE:0.5:6:2880",    # 2 day with interval of 1min
                "RRA:AVERAGE:0.5:60:2016",   # 2 week with interval of 10min
                "RRA:AVERAGE:0.5:180:1488",  # 1 mounth with interval of 30min
                "RRA:AVERAGE:0.5:360:8784"   # 1 year with interval of 1hour
                );
}

package Server;
use strict;
use POSIX;
use IO::Handle;
use HTTP::Daemon;
use HTTP::Status;
use JSON;
#use Data::Dumper;

sub new
{
  my $this = bless {}, shift;
  $this->Debug(3,"");

  # List of files to be delivered
  my @paths = (
    "/",
    "/static.json",
    "/dynamic.json",
    "/status.json",
    "/statistics.json",
    "/menu.json",
    "/friends.json",
    "/version.json",
    "/favicon.ico",

    "/index.html",
    "/statistics.html",
    "/status.html",
    "/shellinabox.html",
    "/cacert.pem",
    "/certificate.p12",
    "/shellinabox",

    "/css/rpimonitor.css",
    "/css/bootstrap.min.css",
    "/css/bootstrap-responsive.min.css",

    "/stat/empty.rrd",

    "/img/preloader.gif",

    "/js/rpimonitor.shellinabox.js",
    "/js/rpimonitor.statistics.js",
    "/js/rpimonitor.status.js",
    "/js/rpimonitor.index.js",
    "/js/rpimonitor.js",
    "/js/jsqrencode.min.js",
    "/js/jquery.min.js",
    "/js/bootstrap.min.js",
    "/js/flot/jquery.flot.min.js",
    "/js/flot/jquery.flot.selection.min.js",
    "/js/flot/jquery.flot.tooltip.min.js",
    "/js/javascriptrrd/binaryXHR.js",
    "/js/javascriptrrd/rrdFlotMatrix.js",
    "/js/javascriptrrd/rrdFlot.js",
    "/js/javascriptrrd/rrdFilter.js",
    "/js/javascriptrrd/rrdMultiFile.js",
    "/js/javascriptrrd/rrdFile.js",
    "/js/javascriptrrd/rrdFlotSupport.js"
  );

  $this->{'paths'} = \@paths;

  return $this;
}

sub Debug
{
  my $this = shift;
  my $level = shift;

  $level <= $verbose or return;
  print STDERR "[", strftime("%Y/%m/%d-%H:%M:%S", localtime), "] ", " " x (5-$level), (caller 1)[3], " @_\n";
}

sub SendFile
{
  my $this = shift;
  my $connection = shift;
  my $file = shift;
  $this->Debug(2,$file);

  $connection->send_file_response($file);
  $connection->close();

  return 1;
<F3>}

sub SendJSON
{
  my $this = shift;
  my $message =shift;

  my $response = HTTP::Response->new(
      RC_OK, OK => [ 'Content-Type' => "application/json" ], $message
  );
  $this->{'connection'}->send_response($response);
  $this->{'connection'}->close();

  return 1;
}

sub SendStatus
{
  my $this = shift;
  my $configuration = shift;
  $this->Debug(2,"");

  my $var = $configuration->{'sharedmem'}->fetch();
  
  $var =~ s/\s+$//g; 
  $this->Debug(3,"JSON: $var");
  $this->SendJSON($var);

  return 1;
}

sub SendRedirect
{
  my $this = shift;
  my $destination = shift;
  $this->Debug(2,$destination);

  $this->{'connection'}->send_redirect($destination, 302);
  $this->{'connection'}->close();

  return 1;
}

sub SendError
{
  my $this=shift;
  my $errorcode= shift;
  $this->Debug(2,$errorcode);

  $this->{'connection'}->send_error($errorcode);
  $this->{'connection'}->close();

  return 1;
}

sub DoGET
{
  my $this = shift;
  my $request = shift;
  my $configuration = shift;
  $this->Debug(2,"");

  my $connection = $this->{'connection'};
  $_ = $request->url->path;
  my $path = $_;
  $this->Debug(2, "$path");

  #The file need to be known or we return an error
  my $isvalid;
  foreach(@{$this->{'paths'}}) {
    if ( $path =~ /$_$/) { 
      $isvalid=1;
      $path=$_;
      last;
    }
  }
  #$isvalid or $connection->send_error(404,"<br><b>$path</b> not in <br>". join ('<br>',@{$this->{'paths'}} )) and return;
  $isvalid or $this->SendError(404);

  $path =~ /shellinabox$/ and $this->SendRedirect( $configuration->{'daemon'}->{'shellinabox'} ) and return;
  $path =~ /dynamic\.json/ and $this->SendStatus( $configuration ) and return;
  $path =~ /([^\/]+)\.json/ and $this->SendJSON($this->{$1}) and return;

  #The main page (/) is requested
  $path =~ /^\/$/ and $path = "/index.html";

  #If the file exists we return it
  -f "$configuration->{'daemon'}->{'webroot'}/$path" and $this->SendFile($connection, "$configuration->{'daemon'}->{'webroot'}/$path") and return;

  $this->Debug(2,"Can't find $configuration->{'daemon'}->{'webroot'}/$path");
  
  #Finally send error
  $this->SendError();
}

sub Run
{
  my $this = shift;
  my $configuration=shift;
  my $monitor=shift;
  $this->Debug(3,"");

  # Add rrds available and status file to the authorized pages
  foreach (glob("$configuration->{'daemon'}->{'webroot'}/img/*.png")){
    /(img\/.*\.png)$/;
    unshift ( @{$this->{'paths'}}, $1);
  }
  @{$this->{'paths'}} = ( @{ $configuration->{'rrdlist'}}, @{$this->{'paths'}} );  
  $this->{'status'} = encode_json(\@{$configuration->{'web'}->{'status'}});
  $this->{'statistics'} = encode_json(\@{$configuration->{'web'}->{'statistics'}});
  $this->{'friends'} = encode_json(\@{$configuration->{'web'}->{'friends'}});
  $this->{'static'} = encode_json(\%{$monitor->{'static'}});
  $this->{'menu'} = encode_json(\%{$configuration->{'menu'}});
  $this->{'version'} = "{\"version\":\"$configuration->{'version'}\"}";
  #print Data::Dumper->Dump([$this->{'paths'}]);
  
  # Create the server
  #print Data::Dumper->Dump([$configuration]);
  $this->{'server'} = new HTTP::Daemon     ( ReuseAddr => 1,
                                             LocalAddr => $configuration->{'daemon'}->{'addr'},
                                             LocalPort => $configuration->{'daemon'}->{'port'})
    or die "Web server not started because of error: $!\n";

  $this->Debug(1,"< URL:", $this->{'server'}->url, ">");

  setgid($configuration->{'daemon'}->{'gid'});
  setuid($configuration->{'daemon'}->{'uid'});

  #Process requests
  for (;;){
    while ( $this->{'connection'} = $this->{'server'}->accept) {
      while (my $request = $this->{'connection'}->get_request) {
        my $method = "Do".$request->method();
        $this->can($method) and $this->$method($request,$configuration);
      }
      $this->{'connection'}->close;
      undef($this->{'connection'});
    }
  }
  $this->Debug(1,"Server stopped");

  $this->{'server'}->close();
}

package Monitor;
use strict;
use POSIX;
use RRDs;
use JSON;
use Scalar::Util qw(looks_like_number);
#use Data::Dumper;

sub new
{
  my $this = bless { }, shift;
  $this->Debug(3,"");
  @{$this->{'files'}} = ("static.json","status.json","statistics.json");
  return $this;
}

sub Debug
{
  my $this = shift;
  my $level = shift;

  $level <= $verbose or return;
  print STDERR "[", strftime("%Y/%m/%d-%H:%M:%S", localtime), "] ", " " x (5-$level), (caller 1)[3], " @_\n";
}

eval 'sub IPC_CREAT {0001000}' unless defined &IPC_CREAT;
eval 'sub IPC_RMID {0}'        unless defined &IPC_RMID;

sub Run
{
  # start main loop
  my $this = shift;
  my $configuration = shift;
  my $serverpid = shift;
  $this->Debug(2,"");

  if ( $configuration->{'daemon'}->{'noserver'}) {
    # write json if server is not running 
    open(FILE, "> $configuration->{'daemon'}->{'webroot'}/static.json")
      or warn $!;
      print FILE encode_json \%{$this->{'static'}} ;
    close(FILE); 
    open(FILE, "> $configuration->{'daemon'}->{'webroot'}/status.json")
      or warn $!;
      print FILE encode_json \@{$configuration->{'web'}->{'status'}} ;
    close(FILE);
    open(FILE, "> $configuration->{'daemon'}->{'webroot'}/statistics.json")
      or warn $!;
      print FILE encode_json \@{$configuration->{'web'}->{'statistics'}} ;
    close(FILE);
  }
  
  #die Data::Dumper->Dump([$this]);

  for(;;)
  {
    # Process data
    $this->Process($configuration,'dynamic');

    # Store and show extracted data
    $this->Status($configuration);

    # Check if server is up 
    if ( !$configuration->{'daemon'}->{'noserver'}) {
      kill(0,$serverpid) or last;
    }
    
    # tempo before next process
    $configuration->{'daemon'}->{'delay'} or last;
    sleep $configuration->{'daemon'}->{'delay'};
  }
  foreach (@{$this->{'files'}}) {
    -f "$configuration->{'daemon'}->{'webroot'}/$_"
      and unlink "$configuration->{'daemon'}->{'webroot'}/$_";
  }
}

sub Process
{
  my $this = shift;
  my $configuration = shift;
  my $list = shift;
  $this->Debug(2,"Processing $list");
 
  $this->{$list} and delete $this->{$list};
  foreach my $kpi ( @{$configuration->{$list}} ) {
    $kpi or next;
    my $file = -f $kpi->{'source'} ? $kpi->{'source'} : "$kpi->{'source'} 2>/dev/null|";
    eval {
      local $SIG{ALRM} = sub { die "Timeout\n" };
      alarm $configuration->{'daemon'}->{'timeout'};
      open(FEED, $file) or die "Can't open $file because $!\n";
      while (<FEED>){
        @_ = /$kpi->{'regexp'}/ or next;
        if ( $kpi->{'postprocess'} ) {
           @_=eval( $kpi->{'postprocess'} );
        }
        my $i=0;
        my @names = split(',',$kpi->{'name'});
        foreach ( @_ ) { 
          $this->{$list}->{$names[$i]}=$_[$i];
          $i++;
        }
      }
      close(FEED);
      alarm 0;
    }
  }
  #print Data::Dumper->Dump([$list]);
  #print Data::Dumper->Dump([$this->{'dynamic'}]);
}

sub Status
{
  my $this = shift;
  my $configuration = shift;
  $this->Debug(2,"");

  $this->{'dynamic'} or return;

  my ($sec,$min,$hour,$mday,$mon,$year) = (localtime)[0,1,2,3,4,5];
  @{$this->{'dynamic'}->{'localtime'}}=($year+1900,$mon+1,$mday,$hour,$min,$sec);

  my $json=encode_json \%{$this->{'dynamic'}};
  $this->Debug(4,"\n$json");

  # if embeded server is not used, we write the json file else the
  # data are transmitted with a shared memory
  if ( $configuration->{'daemon'}->{'noserver'} == 1 ){
    push (@{$this->{'files'}},"dynamic.json");
    open(FILE, "> $configuration->{'daemon'}->{'webroot'}/dynamic.json")
      or warn $!;
      print FILE $json ;
    close(FILE);
  }
  else{
    # write current status (JSON) in shared memory
    $configuration->{'sharedmem'}->store( $json );
  }

  # add data in round robin database
  #print Data::Dumper->Dump($configuration->{'rrd'});
  foreach (@{$configuration->{'rrd'}}){
    foreach my $name ( split(',',$_->{'name'}) ) {
      if ( looks_like_number( $this->{'dynamic'}->{$name} ) ) { 
        RRDs::update("$configuration->{'daemon'}->{'webroot'}/stat/$name.rrd", "N:".$this->{'dynamic'}->{$name});
      }
    }
  }
}

package main;
use strict;
use POSIX;

sub help
{
  die <<EOF;
NAME
  rpimonitord

USAGE
  rpimonitord [-a address][ -b pidfile][[-c configuration_file]...]
     [-d delay][-h][-n][-p port][-v[v[...]]][-t timeout][-V]

DESCRIPTION
  rpimonitord is designed to extract data from Raspberry Pi system and 
  store statistic into RRD. rpimonitord embed a web server allowing 
  to display current status and statistics.

OPTIONS
  -a, --addr        Web server bind address (Default: 0.0.0.0)
  -b, --background  Define the pid file when run in background
  -c, --conf        Define the configuration file
                     (Default: /etc/rpimonitord.conf and 
                               /etc/rpimonitord.conf.d/*.conf)
  -d, --delay       Delay between check ins seconds (Default: 10)
                      Note: If you want to change the default delay, the
                      rrd file will have to be deleted rpimonitord will
                      recreate them at next startup with the new time
                      slice.
  -h, --help        Shows this help and exit
  -n, --noserver    Don't start embeded server
  -p, --port        Web server port (Default: 8888)
  -s, --show        Show configuration as loaded and exit
  -t, --timeout     KPI read timeout in seconds (Default: 5)
  -v, --verbose     Write debug info on screen
  -V, --Version     Show version and exit

CONFIGURATION
  Configuration can be defined into /etc/rpimonitord.conf and
  /etc/rpimonitord.conf.d/*.conf or in a list of files specified
  by -c parameter. See the /etc/rpimonitord.conf and 
  /etc/rpimonitord.conf.d/default.conf files provided with at
  installation to see how to customize rpimonitord.
  Configuration defined inside a configuration file always overwrite
  default values. 
  Configuration given as option of the command line always overwrite 
  the one defined into a file.

RAW DATA ACCESS
  Once RPi-Monitor is started the data representing the current status
  are available in json format and can be downloaded from the root of
  the web interface (ex: http://RpiAddress:8888/static.json)
    static.json:    Static information extracted at startup
    dynamic.json:   Current values of dynamic information extracted 
                    periodically
    menu.json:      Description of menus when multiple pages are 
                    configured

  The web interface configuration can also be downloaded in json format:
    statistics.json: Description of statistics page
    status.json:    Description of status page
    friends.json:   List of friends

  Statistic information are stored into RRD file available in the
  directory [webroot]/stat/

AUTHOR
  Xavier Berger
    http://rpi-experiences.blogspot.fr

EOF
}

sub writePID {
  open(PID, ">> $pidfile") || die "Could not open '$pidfile' $!"; 
  print PID "$$\n"; 
  close(PID); 
}

sub daemonize {
  chdir '/' or die "Can't chdir to /: $!";
  open STDIN, '/dev/null' or die "Can't read /dev/null: $!";
  open STDOUT, '>>/dev/null' or die "Can't write to /dev/null: $!";
  open STDERR, '>>/dev/null' or die "Can't write to /dev/null: $!";
  defined(my $pid = fork) or die "Can't fork: $!";
  exit if $pid;
  setsid or die "Can't start a new session: $!";
  umask 0;
}

sub StartServer
{
  #start server as a separate process with specific user right (user:pi)
  my $configuration = shift;
  my $monitor = shift;
  my $serverpid;
  unless ( $configuration->{'daemon'}->{'noserver'} ){
    unless ($serverpid = fork) {
      $pidfile and writePID();
      my $server = Server->new();
      $server->Run($configuration,$monitor);
      exit;
    }
  }
  return $serverpid;
}

my $configuration = Configuration->new();

sub stop
{
  $configuration->{'daemon'}->{'delay'} = 0;
}

# read command lines
while($_ = shift)
{
  /-a(ddr)?/        and $configuration->{'daemon'}->{'addr'} = shift and next;
  /-b(ackground)?/  and $pidfile = shift and next;
  /-c(onf)?/        and push @{$configuration->{'daemon'}->{'confFiles'}}, shift and next;
  /-d(elay)?/       and $configuration->{'daemon'}->{'delay'} = shift and next;
  /-h(elp)?/        and help;
  /-n(oserver)?/    and $configuration->{'daemon'}->{'noserver'}++ and next;
  /-p(ort)?/        and $configuration->{'daemon'}->{'port'} = shift and next;
  /-s(how)?/        and $configuration->{'show'}++ and next; 
  /-t(imeout)?/     and $configuration->{'daemon'}->{'timeout'} = shift and next;
  /^-([v]+)$/       and $verbose = length $1 and next;
  /-V(ersion)?/     and die "$0 version $VERSION\n";
} 
$configuration->Load();

$pidfile and &daemonize;

my $monitor = Monitor->new();
# Process data that should be extracted once, before the server starts
$monitor->Process($configuration,'static');


my $serverpid;

my $startTime = 0;
for (;;) {
  my $currentTime=mktime(localtime());
  ( $currentTime - $startTime ) < 2 and die "rpimonitord stopped because respawning too fast.\n";
  $startTime = $currentTime;
  if ( $pidfile ) {
    -f $pidfile and unlink $pidfile;
    writePID();
  }
 
  $serverpid = StartServer($configuration,$monitor);
  #Start process respawner
  if ( my $procpid = fork() ) {
    waitpid($procpid,0);
  }
  else{
    $pidfile and writePID();
    $monitor->Run($configuration, $serverpid);
    exit(0);
  }
  $serverpid and kill (9,$serverpid);
  $configuration->{'daemon'}->{'delay'} or last; #delay == 0, it means we should stop the process.
}
